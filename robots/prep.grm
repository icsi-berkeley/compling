general construction Preposition
   subcase of Word
	
 
 general construction SpatialPreposition
   subcase of Preposition
   meaning: TrajectorLandmark  


general construction PathPreposition
   subcase of SpatialPreposition 
   meaning: SPG 


general construction LocativePreposition
   	subcase of SpatialPreposition


construction IN-Locative // 11/4/13
   subcase of LocativePreposition
   form
      constraints
         self.f.orth <-- "in" 
   meaning  
     evokes BoundedObject as bo
     constraints
         self.m.landmark.ontological-category <-- @container 
         self.m.landmark <--> bo.boundedObject
         self.m.profiledArea <--> bo.interior
       
 
 construction AT-Locative
   subcase of LocativePreposition 
   form
      constraints
         self.f.orth <-- "at"
   meaning 
      evokes Proximity as p
      constraints
       self.m.landmark.ontological-category <--@region
       self.m.profiledArea <--> p.proximalArea 
       self.m.landmark <--> p.center   
 

construction NEAR-Locative  // trying 11/26/12
   subcase of LocativePreposition //, Loc-PP 
   form
      constraints
         self.f.orth <-- "near"
   meaning: TrajectorLandmark 
      evokes Proximity as p
      constraints
        self.m.profiledArea <--> p.proximalArea 
        self.m.landmark <--> p.center 
        self.m.landmark.ontological-category <-- @region
        
construction TO-Path
   subcase of PathPreposition
   form
      constraints
         self.f.orth <-- "to"
   meaning: SPG  
     constraints
        self.m.landmark <--> self.m.goal

construction  INTO-Path // container 11/5/13
   subcase of PathPreposition
   form
      constraints
         self.f.orth <-- "into"
   meaning: SPG  
      evokes BoundedObject as bo
      constraints 
          self.m.landmark.ontological-category <-- @container  
          self.m.landmark <--> bo.boundedObject
         self.m.goal <--> bo.interior
         self.m.source <--> bo.exterior
  
  
// 7/21/14 (ST), for testing purposes; "move behind the block" 

construction BEHIND-Path
	subcase of PathPreposition
	form
	   constraints
	     self.f.orth <-- "behind"
	meaning: SPG
	   evokes Proximity as p
	   evokes Sidedness as side	   
	   constraints
	      //self.m.profiledArea <--> self.m.landmark
	      side.back <--> self.m.goal
	      self.m.landmark <--> self.side.obj
	      self.m.landmark <--> self.p.center
	      self.m.goal <--> p.proximalArea
	      self.m.goal.ontological-category <-- @region
	      

// 7/23/14 (ST), "the box is behind the block", etc.       	      
construction BEHIND-Locative
	subcase of LocativePreposition
	form
	   constraints
	     self.f.orth <-- "behind"
	meaning: TrajectorLandmark
	   evokes Proximity as p
	   evokes Sidedness as side
	   constraints
	   	  self.m.profiledArea.ontological-category <-- @back  // how to tie "profiledArea" to "back"??
	      side.back <--> self.m.profiledArea   
	      self.m.landmark <--> self.side.obj
	      self.m.landmark <--> self.p.center
	      self.m.profiledArea <--> p.proximalArea
	      //self.m.trajector <--> p.proximalArea
	      self.m.landmark.ontological-category <--@region
	      	      
// 7/21/14 (ST), for testing purposes: "move to the North side of the red block, etc."

general construction ModifierPreposition
   subcase of Preposition
   meaning: Modification
   
construction OF-Modifier
   subcase of ModifierPreposition
   form
      constraints
         self.f.orth <-- "of"      
        
	      
	      
	      
	      
