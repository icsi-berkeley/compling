/*
******************************
CONSTRUCTIONAL AND SCHEMATIC GLUE
DEFINED BY JOHNO

CONSTRUCTIONS AND SCHEMAS FOUND
IN THIS FILE:
-RD
-WordForm
-TrajectorLandmark
-SPG
-ContainerSchema
-HouseSchema
-Animate
-BodyPart
-Legs
-Person
-DogSchema
-S
-NPVP
-NP
-DetNoun
-Determiner
-The
-Noun
-House
-Dog
-ProperName
-Eve
-VP
-Verb
-SpatialPP
-SpatialPreposition
-Into
-WalkedVerb
-CarriedVerb
******************************
*/

/*
******************************
GLUE SCHEMAS

Schemas found in this section
-RD
-WordForm
-TrajectorLandmark
-SPG
-ContainerSchema
-HouseSchema
-Animate
-BodyPart
-Legs
-Person
-DogSchema

******************************
*/


schema RD
  roles
    category
    gender
    count
    specificity
    resolved-ref


schema WordForm
  orth


schema TrajectorLandmark
  trajector
  landmark


schema SPG
  subcase of TrajectorLandmark
  source
  path
  goal


schema ContainerSchema
  interior
  boundary
  portal


schema HouseSchema
  subcase of ContainerSchema


schema Animate


schema BodyPart
  roles
    owner:Animate


schema Legs
  subcase of BodyPart


schema Person
  subcase of Animate
  name


schema DogSchema
  subcase of Animate

/*
******************************
Constructions found in this section:
-S
-NPVP
-NP
-DetNoun
-Determiner
-The
-Noun
-House
-Dog
-ProperName
-Eve
-VP
-Verb
-SpatialPP
-SpatialPreposition
-Into
-WalkedVerb
-CarriedVerb
******************************
*/


abstract construction S
 constructional
   constituents  
     vp:VP
 meaning: Event
  constraints
  self.m <--> vp.m


construction NPVP
 subcase of S
 constructional
   constituents  
     subj: NP
 form
   constraints
   subj.f before vp.f
 meaning
  constraints
  self.m.profiled-participant <--> subj.m.category


abstract construction NP
  meaning:RD  


construction DetNoun
  subcase of NP
  constructional
    constituents
      d:Determiner
      n:Noun
  form
    constraints
      d.f before n.f
  meaning
    constraints
      self.m <--> d.rd
      self.m.category <--> n.m
      //self.m.resolved-ref <--> n.m


abstract construction Determiner
  meaning
    evokes RD as rd


construction The
  subcase of Determiner
  form:WordForm
    constraints
      self.f.orth <-- "the"
  meaning
    constraints
      rd.specificity <-- "mentioned"
	

abstract construction Noun
  form:WordForm


construction House
  subcase of Noun
  form
    constraints
    self.f.orth <-- "house"
  meaning:HouseSchema

construction Dog
  subcase of Noun
  form
    constraints
    self.f.orth <-- "dog"
  meaning:DogSchema


abstract construction ProperName
  subcase of NP


construction Eve
  subcase of ProperName
  form:WordForm
    constraints
    self.f.orth <-- "eve"
  meaning
    evokes Person as p
    constraints
      p.name <-- "eve"
      self.m.category <--> p
      self.m.resolved-ref <--> p


abstract construction VP
  constructional
    constituents
      v:Verb
  meaning:Event
    constraints
      self.m.profiled-process <--> v.m


abstract construction Verb
  form:WordForm
  meaning:Process


construction SpatialPP
  constructional
    constituents
      prep:SpatialPreposition
      landmark:NP
  form
    constraints
      prep.f meets landmark.f
  meaning:TrajectorLandmark
    constraints
      self.m <--> prep.m
      self.m.landmark <--> landmark.m.category


abstract construction SpatialPreposition
  form:WordForm
  meaning:TrajectorLandmark

construction Into
  subcase of SpatialPreposition
  form
    constraints
      self.f.orth <-- "into"
  meaning:SPG
    evokes ContainerSchema as c
    constraints
      self.m.landmark <--> c
      self.m.goal <--> c.interior


construction WalkedVerb
  subcase of Verb
  form
   constraints
     self.f.orth <--"walked"
  meaning:WalkAction


construction CarriedVerb
  subcase of Verb
  form
   constraints
     self.f.orth <--"carried"
  meaning:CarryAction

