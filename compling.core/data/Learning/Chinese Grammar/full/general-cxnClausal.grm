//============================================================
// Everything in this section has to be learned
//============================================================
 
//============================================================
// Constructions: Abstract Verb Phrase Constructions
//============================================================

// can either take a post-verbal aspect marker (le, zhe) or a resultative phrase (stative or otherwise)

// The Abstract supertype of all verb clauses, this construction does not specify much by 
// way of constructional constituents, to allow for recursive (e.g. serial) VPs. 

Abstract Construction Verb_Clause
	meaning: Process
		evokes Event_Descriptor as ed
		evokes Discourse_Segment as DS


// The Abstract supertype of the VPs with a single verb (by design, serial verbs require recusion,
// except for the "resultative verb phrases" (see Li & Thompson), which are specified as subtypes 
// of the Postverbal_Resultative_Phrase


Construction Preverbal_Modifications
	constructional
		constituents
			optional neg : Negation_Marker
			optional loc_phrase : Preverbal_Locative_Phrase
			optional inst_phrase : Preverbal_Instrumental_Phrase
			optional gei_phrase : Preverbal_Gei3_Phrase
			optional pre_adv : Preverbal_Adverbial
	meaning
		evokes Event_Descriptor as ed
			constraints
			ed.spatial_setting <--> loc_phrase.m.landmark
			ed.modifiers <--> pre_adv.m
			ed.event_type <--> gei_phrase.process
			ed.event_type <--> neg.m.process
			ed.event_type <--> inst_phrase.process
			ed.profiled_participant <--> gei_phrase.protagonist


Abstract Construction Intransitive_VP
	subcase of Verb_Clause
	meaning: Process
		constraints
			ed.event_type <--> self.m


Abstract Construction Intransitive_VP_with_Single_Verb
	subcase of Intransitive_VP
	constructional
		constituents
			v : Verb
			 optional post_asp : Postverbal_Aspect_Marker
	form
		constraints
			v.f before post_asp.f	
	meaning
		constraints
			ed.profiled_process  <--> v.m
			ed.event_structure <--> v.event_structure
			ed.event_structure <--> post_asp.event_structure
			DS.speech_act <--> v.speech_act	


Construction Intransitive_VP_with_Mod
	subcase of Intransitive_VP
	constructional
		constituents
			pm : Preverbal_Modifications	
			vc : Intransitive_VP	
	form
		constraints
			pm.f before vc.f
	meaning: Process
		constraints
			DS <--> vc.DS
			ed <--> vc.ed
			ed <--> pm.ed
			self.m <--> vc.m			


Abstract Construction Transitive_VP
	subcase of Verb_Clause
	meaning: Process
		evokes Proto_Transitive as prototransitive
		constraints
			prototransitive.proto_agent <--> self.m.protagonist
			ed.event_type <--> self.m


Abstract Construction Transitive_VP_with_Single_Verb
	subcase of Transitive_VP
	constructional
		constituents
			v : Verb
			optional post_asp : Postverbal_Aspect_Marker
	form
		constraints
			v.f before post_asp.f
	meaning
		constraints
			ed.profiled_process  <--> v.m
			ed.event_structure <--> v.event_structure
			ed.event_structure <--> post_asp.event_structure
			DS.speech_act <--> v.speech_act	


Construction Transitive_VP_with_Mod
	subcase of Transitive_VP
	constructional
		constituents
			pm : Preverbal_Modifications	
			vc : Transitive_VP	
	form
		constraints
			pm.f before vc.f
	meaning: Process
		constraints
			DS <--> vc.DS
			ed <--> vc.ed
			ed <--> pm.ed
			self.m <--> vc.m


Construction Transitive_VP_with_Fronted_Object
	subcase of Verb_Clause
	constructional
		constituents
			ba : ba3-CV
			extraposed obj : Ref_Exp
			vc : Transitive_VP
	form
		constraints
			ba.f meets obj.f
			obj.f meets vc.f
	meaning
		constraints
			DS <--> vc.DS
			self.m <--> vc.m
			ba.prototransitive <--> vc.prototransitive
			ba.prototransitive.proto_patient <--> obj.m


Abstract Construction Ditransitive_VP
	subcase of Transitive_VP


Abstract Construction Ditransitive_VP_with_Single_Verb
	subcase of Ditransitive_VP, Transitive_VP_with_Single_Verb
	constructional
		constituents
			n1 : Ref_Exp [.7, .7]
	form
		constraints
			post_asp.f before n1.f

//============================================================
// Constructions: Intransitive Verb Phrase Constructions
//============================================================


// 胳膊疼 (attested)
// 餓不餓 (attested)
// 哪個壞了	(attested)
// Aside: 她正在高興 (should in theory take care of this, but right now it doesn't take a pre-verbal aspect marker)
// The protagonist is limited to entities to exclude sentences like 他唱歌很好聽, where the stative modifies the manner of the process

Construction Intransitive_State_VP
	subcase of Intransitive_VP_with_Single_Verb
	meaning: Intransitive_State
		constraints
			self.m <--> v.m	// notice Verb includes A-Not-A
			self.m.protagonist <-- @Entity
			ed.affectedness <-- @Not_Affected


// 她高興起來了
// Aside: 一夜之間老了好幾歲  (should in theory take care of this, but right now it doesn't take a quantity NP)

Construction Intransitive_State_Change_VP1
	subcase of Intransitive_VP_with_Single_Verb
	constructional
		constituents
			 result : Directional_Phrase
	form
		constraints
			result.f before post_asp.f
	meaning: State_Change
		evokes Intransitive_State as ins
		constraints
			ins <--> v.m
			self.m.resulting_state <--> v.m
			ed.event_structure <--> result.event_structure
			ed.affectedness <-- @Affected


// 這車壞掉了
// 她氣昏了
// Aside: 她氣昏了頭 (should in theory take care of this, but now the stative resultative doesn't take an object)

Construction Intransitive_State_Change_VP2
	subcase of Intransitive_VP_with_Single_Verb
	constructional
		constituents
			 result : Stative_Resultative_Phrase
	form
		constraints
			result.f before post_asp.f
	meaning: State_Change
		evokes Intransitive_State as ins
		constraints
			ins <--> v.m
			self.m.means <--> v.m
			self.m.resulting_state <--> result.m
			self.m.protagonist <--> v.m.protagonist
			v.m.protagonist <--> result.m.protagonist
			ed.event_structure <--> result.event_structure
			ed.affectedness <-- @Affected


// Three subcases. One with NP_arg, and two with S complements.  The recursive ones are defined in the next section.
// Tehnically, the object in a mental state VP can be extracted _if_ a stative resultative is also used (thereby changing the affectedness of the object)

Abstract Construction Mental_State_VP
	subcase of Intransitive_VP_with_Single_Verb
	meaning: Mental_State
		constraints
			self.m <--> v.m
			ed.affectedness <-- @Not_Affected


// 想不想倩倩了	(attested)
// 不管噢	(attested)

Construction Mental_State_with_NP_arg
	subcase of Mental_State_VP
	constructional
		constituents
			n : Ref_Exp [.7, .7]
	form
		constraints
			post_asp.f before n.f
	meaning
		constraints
			self.m.percept <--> n.m


// 先提提褲子我才讓你玩呢	(attested)
// 摔跤就不許哭	(attested)
// 她跑掉了

Construction Intransitive_Action_VP
	subcase of Intransitive_VP_with_Single_Verb
	constructional
		constituents
			 optional result : Stative_Resultative_Phrase
	form
		constraints
			result.f before post_asp.f
	meaning: Intransitive_Action		// This will "overfit" and accept verbs with translational self motion meaning too.
		constraints
			self.m <--> v.m
			self.m.protagonist <--> result.m.protagonist
			ed.event_structure <--> result.event_structure


// 阿姨藏起來呢	(attested)
// 快點起來	(attested)

Construction Self_Motion_State_Change
	subcase of Intransitive_VP_with_Single_Verb
	constructional
		constituents
			 result : Directional_Phrase
	form
		constraints
			result.f before post_asp.f
	meaning: State_Change
		evokes Self_Motion as sm
		constraints
			sm <--> v.m
			self.m.means <--> v.m
			ed.event_structure <--> result.event_structure
			ed.affectedness <-- @Not_Affected


// 走到哪邊去
// 出去了
// 又跑這來 (attested, but this is an awkward usage)

Construction Translational_Self_Motion_VP1
	subcase of Intransitive_VP_with_Single_Verb
	constructional
		constituents
			result : Motion_Resultative_Phrase
	form
		constraints
			result.f before post_asp.f
	meaning: Translational_Self_Motion
		constraints
			self.m <--> v.m
			self.m.spg <--> result.m
			ed.event_structure <--> result.event_structure


// 藏哪後頭去了
// 你下地媽給你盛好嗎

Construction Translational_Self_Motion_VP2
	subcase of Intransitive_VP_with_Single_Verb
	constructional
		constituents
			loc : Loc_Exp	// thus the only way to use this construction with an object is to displace the object
			optional dp : Directional_Phrase
		constraints
			v.motion_Focus <-- @Path_Focus
	form
		constraints
			post_asp.f before loc.f
			loc before dp.f
	meaning: Translational_Self_Motion
		constraints
			self.m <--> v.m
			self.m.spg.goal <--> loc.m.landmark
			self.m.spg <--> dp.m


// 她哭著鬧著往外跑	(attested)

Construction Translational_Self_Motion_VP3
	subcase of Intransitive_VP_with_Single_Verb
	constructional
		constituents
			cvp : Preverbal_Heading_Phrase
	form
		constraints
			cvp.f before v.f
	meaning: Translational_Self_Motion
		constraints
			self.m <--> v.m
			self.m.spg <--> cvp.m


//============================================================
// Constructions: Transitive Verb Phrase Constructions
//============================================================


Abstract Construction Complex_Motion_VP
	subcase of Transitive_VP_with_Single_Verb
	constructional
		constituents
			n : Ref_Exp [.7, .7]
	form
		constraints
			post_asp.f before n.f
	meaning : Complex_Process
		constraints
			ed.affectedness <-- @Affected
			self.m.process2.protagonist <--> n.m
			prototransitive.proto_patient <--> self.m.process2.protagonist


// 你都拿過來了	(attested)
// 背著書包去上學啊	(attested)

// 東西多的話用貨櫃裝過去 (attested online, though uncommon)
// 計劃用一個巨大無比的箱子把所有的禮物都裝過去 (attested online, though uncommon)

Construction Joint_Motion_VP
	subcase of Complex_Motion_VP
	constructional
		constituents	
			result : Motion_Resultative_Phrase
	form
		constraints
			n.f before result.f
	meaning : Joint_Motion
		constraints
			self.m.cfa <--> v.m


// 拉會車
// 累了就踢會球吧
// 還給我搓

Construction Caused_Motion_VP1
	subcase of Complex_Motion_VP
	meaning : Caused_Motion	// recall that Caused_Motion is a subcase of Cause_Effect
		constraints
			self.m.cause <--> v.m


// 都扔到房頂上去了你	(attested)
// 快擱進去	(attested)

// 搓球盡量抓住球起跳點就把球切搓過去 (attested online, table tennis usage?)
// 搓進去痛...抽血時痛...拔出來也痛 (attested online, pricking motion?)

Construction Caused_Motion_VP2
	subcase of Complex_Motion_VP
	constructional
		constituents	
			result : Motion_Resultative_Phrase
	form
		constraints
			n.f meets result.f
	meaning : Caused_Motion	// recall that Caused_Motion is a subcase of Cause_Effect
		constraints
			self.m.cause <--> v.m
			ed.event_structure <--> result.event_structure
			self.m.motion.spg <--> result.m


// 你爸給你擱哪去了
// 大個的給阿姨擱手裡

Construction Caused_Motion_VP3
	subcase of Complex_Motion_VP
	constructional
		constituents
			n : Ref_Exp [.7, .0]
			loc : Loc_Exp	// the only way to use this construction with an object is to displace the object
			optional dp : Directional_Phrase
		constraints
			v.motion_Focus <-- @Path_Focus
	form
		constraints
			v.f meets loc.f
			loc.f meets dp.f
	meaning : Caused_Motion	// recall that Caused_Motion is a subcase of Cause_Effect
		constraints
			self.m.cause <--> v.m
			self.m.motion.spg.goal <--> loc.m.landmark
			self.m.motion.spg <--> dp.m


// 你的鞋往哪擱	(attested)
// 你把哪根皮不許給我往地下扔噢	(attested)

Construction Caused_Motion_VP4
	subcase of Complex_Motion_VP
	constructional
		constituents	
			cvp : Preverbal_Heading_Phrase
	form
		constraints
			cvp.f before v.f
	meaning : Caused_Motion	// recall that Caused_Motion is a subcase of Cause_Effect
		constraints
			self.m.cause <--> v.m
			self.m.motion.spg <--> cvp.m


// 這又給我毀	(attested)
// 你爸給你扔啦	(attested)

Construction Central_Transitive_VP
	subcase of Transitive_VP_with_Single_Verb
	constructional
		constituents
			optional result : Stative_Resultative_Phrase
			n : Ref_Exp [.7, .7]
	form
		constraints
			result.f before post_asp.f
			post_asp.f before n.f
	meaning : Cause_Effect
		evokes Cause_Change as cause_change
		constraints
			cause_change <--> v.m
			self.m.cause <--> v.m
			self.m.effect <--> result.m
			prototransitive.proto_patient <--> self.m.effect.protagonist


//FIXME: I think in some dialects it can take CV argument for recipient

Abstract Construction Obtainment_VP
	subcase of Transitive_VP_with_Single_Verb
	meaning : Obtainment
		constraints
			self.m <--> v.m
			prototransitive.proto_patient <--> self.m.obtained


// 給你留著呢	(attested)
// 把球拿過來	(attested)

Construction Obtainment_with_NP_arg
	subcase of Obtainment_VP
	constructional
		constituents
			n : Ref_Exp [.7, .7]
	form
		constraints
			post_asp.f before n.f
	meaning
		constraints
			self.m.obtained <--> n.m


// 你少吃點	(attested)
// 喝碗水吧	(attested)

Construction Ingestion_VP
	subcase of Transitive_VP_with_Single_Verb
	constructional
		constituents
			optional result : Stative_Resultative_Phrase
			n : Ref_Exp [.7, .7]
	form
		constraints
			result.f before post_asp.f
			post_asp.f before n.f
	meaning : Ingestion
		constraints
			self.m <--> v.m
			self.m.ingested <--> n.m
			self.m.protagonist <--> result.m.protagonist
			prototransitive.proto_patient <--> self.m.ingested
			ed.event_structure <--> result.event_structure


Abstract Construction Perception_VP
	subcase of Transitive_VP_with_Single_Verb
	meaning : Perception
		constraints
			self.m <--> v.m
			prototransitive.proto_patient <--> self.m.percept			


// 聽見了嗎	(attested)
// 你看到電燈了吧	(attested)

Construction Perception_with_NP_arg
	subcase of Perception_VP
	constructional
		constituents
			optional result : Stative_Resultative_Phrase
			n : Ref_Exp [.7, .7]
	form
		constraints
			result.f before post_asp.f
			post_asp.f before n.f
	meaning
		constraints
			self.m.percept <--> n.m
			self.m.protagonist <--> result.m.protagonist
			ed.event_structure <--> result.event_structure


// 你穿小的了吧	(attested)
// 開始吧	(attested)

Construction Uncategorized_Transitive_VP
	subcase of Transitive_VP_with_Single_Verb
	constructional
		constituents
			optional result : Stative_Resultative_Phrase
			n : Ref_Exp [.7, .7]
	form
		constraints
			result.f before post_asp.f
			post_asp.f before n.f
	meaning : Uncategorized_Transitive_Action
		constraints
			self.m <--> v.m
			self.m.patient <--> n.m
			self.m.protagonist <--> result.m.protagonist
			prototransitive.proto_patient <--> self.m.patient
			ed.event_structure <--> result.event_structure


//============================================================
// Constructions: Ditransitive Verb Phrase Constructions
//============================================================


// 把這個給阿姨噢	(attested)
// 餵	(attested)

Construction Ditransitive_Transfer_VP
	subcase of Ditransitive_VP_with_Single_Verb
	constructional
		constituents
			n2 : Ref_Exp [.7, .7]
	form
		constraints
			n1.f meets n2.f
	meaning: Transfer
		constraints		
			self.m <--> v.m	// must be some subtype of Transfer verb
			self.m.recipient <--> n1.m
			self.m.theme <--> n2.m
			prototransitive.proto_patient <--> self.m.theme

/*
// 我問你一個問題 (this pattern is grammatical but is not attested in the corpus)

Construction Ditransitive_Communication_VP_with_NP_arg
	subcase of Ditransitive_VP_with_Single_Verb
	constructional
		constituents
			n2 : Ref_Exp [.7, .7]
	form
		constraints
			n1.f meets n2.f
	meaning: Communication
		constraints		
			self.m <--> v.m	// must be some subtype of Communication verb
			self.m.audience <--> n1.m
			self.m.communicated <--> n2.m
			prototransitive.proto_patient <--> self.m.communicated



// 把這個送給你
// 把球拿給我 (this pattern is grammatical to me but is not attested in the corpus)

Construction Transitive_VP_with_Recipient
	subcase of Transitive_VP_with_Single_Verb
	constructional
		constituents
			gei : gei3-CV2
			rec : Ref_Exp
	form
		constraints
			v.f meets post_asp.f
			obj.f meets gei.f
			gei.f meets rec.f
	meaning
		evokes Transfer as transfer
		constraints
			transfer.giver <--> v.m.protagonist
			transfer.recipient <--> rec.m
			transfer.theme <--> obj.m
*/

//============================================================
// Constructions: Verb Phrases with recursion
//============================================================


// 就愛看哪個車	(attested)
// 你要穿上這鞋	(attested)

Construction Mental_State_with_Scomp1
	subcase of Mental_State_VP
	constructional
		constituents
			vc : Verb_Clause
	form
		constraints
			post_asp.f before vc.f
	meaning
		constraints
			self.m.percept <--> vc.ed.referent
			self.m.protagonist <--> vc.m.protagonist 
			DS <--> vc.DS


// 你爸打你怕不怕呀	(attested)

Construction Mental_State_with_Scomp2
	subcase of Mental_State_VP
	constructional
		constituents
			vc : Voicing
	form
		constraints
			post_asp.f before vc.f
	meaning
		constraints
			self.m.percept <--> vc.m.referent
			DS <--> vc.DS



// 看媽媽別花花	(attested)
// this is the "observe someone do something" sense
// FUTURE: somehow the semantics doesn't seem quite right -- the whole process is the percept

Construction Perception_with_Scomp 	
	subcase of Perception_VP
	constructional
		constituents
			vc : Voicing
	form
		constraints
			post_asp.f before vc.f
	meaning
		constraints
			self.m.percept <--> vc.m.referent
			DS <--> vc.DS


// 吃完咱們找倩倩玩去	(attested)
// 咱們找倩倩看電影去	
// 咱們找東西吃去
// FUTURE: notice that there are two semantic patterns here (the obtained object being the agent vs patient of the vc)
// I believe that this relation is inferred, since the obtained object can be an adjunct as well (e.g. "find a pen write")
// what about in English? ("find a place to sit", "find a time to meet", "find a pen to write with")

Construction Obtainment_with_Scomp
	subcase of Obtainment_VP
	constructional
		constituents
			r : Ref_Exp
			vc : Verb_Clause
	form
		constraints
			post_asp.f before r.f
			r.f before vc.f
	meaning : Obtainment
		constraints
			self.m.obtained <--> r.m
			vc.m.protagonist <--> self.m.protagonist
			DS <--> vc.DS


// 你讓阿姨吃	(attested)

Construction Ditransitive_Communication_VP_with_Scomp1
	subcase of Ditransitive_VP_with_Single_Verb
	constructional
		constituents
			vc : Verb_Clause
	form
		constraints
			n1.f meets vc.f
	meaning: Communication
		constraints
			self.m <--> v.m	// must be some subtype of Communication verb
			self.m.audience <--> n1.m
			self.m.audience <--> vc.m.protagonist 
			self.m.communicated <-- @Process
			self.m.communicated <--> vc.ed.referent
			prototransitive.proto_patient <--> self.m.communicated


// 我問你這筆給誰	(attested)
// 你告我裝甚麼呀	(attested)

Construction Ditransitive_Communication_VP_with_Scomp2
	subcase of Ditransitive_VP_with_Single_Verb
	constructional
		constituents
			vc : Voicing
	form
		constraints
			n1.f meets vc.f
	meaning: Communication
		constraints		
			self.m <--> v.m	// must be some subtype of Communication verb
			self.m.audience <--> n1.m
			self.m.communicated <--> vc.m.referent
			prototransitive.proto_patient <--> self.m.communicated


Construction Generic_Serial_Verb_Clauses
	subcase of Verb_Clause
	constructional
		constituents
			vc1 : Verb_Clause
			vc2 : Verb_Clause
	form
		constraints
			vc1.f before vc2.f
	meaning: Complex_Process
		constraints
			DS <--> vc1.DS
			DS <--> vc2.DS
			vc1.ed.event_structure.modifier_aspect <-- @Non_Ongoing_Modifier_Aspect
			self.m.process1 <--> vc1.m
			self.m.process2 <--> vc2.m
			self.m.protagonist <--> vc1.m.protagonist 
			vc1.m.protagonist <--> vc2.m.protagonist


//============================================================
// Constructions: Subject_VP
//============================================================

Abstract Construction Voicing
	meaning: Event_Descriptor
		evokes Discourse_Segment as DS


Construction Active_Subject_VP
	subcase of Voicing
	constructional
		constituents
			s : Ref_Exp [.2, .999]
			vc : Verb_Clause
			optional crs : le-SP
	form
		constraints
			s.f meets vc.f
			vc.f meets crs.f
	meaning
		constraints
			DS <--> vc.DS
			self.m <--> vc.ed
			self.m.event_type <--> vc.m
			self.m.profiled_participant <--> vc.m.protagonist
			s.m <--> vc.m.protagonist


//============================================================
// Constructions: Clauses
//============================================================

Abstract Construction Finite_Clause
	subcase of Important_Type
	constructional
		constituents
			optional ex : Interjection
			vc : Voicing
	meaning: Event_Descriptor
		evokes Discourse_Segment as DS
		constraints
			DS <--> vc.DS
			self.m <--> vc.m


// these should handle both declarative and questions
// technically there are two ways of creating a question:
// 1) VC + the SFP ma
// 2) an A-not-A VC + an optional SFP ne
// note that while both ma and A-not-A denote question speech acts, 
// they cannot be used together. These constructions below are thus
// overly admissive.

Construction Finite_Clause_SFP
	subcase of Finite_Clause
	constructional
		constituents
			sfp : Sentence_Final_Particle
	form
		constraints			
			vc.f meets sfp.f
	meaning
		constraints
			DS.speech_act <--> sfp.m


Construction Finite_Clause_noSFP
	subcase of Finite_Clause
	meaning
		constraints
			DS.speech_act.forcefulness <-- @Normal
	

Construction Neg_Imperative
	subcase of Finite_Clause
	constructional
		constituents
			b : bie2
	form
		constraints
			b.f meets vc.f
	meaning
		evokes RD as rd
		constraints
			vc.m.profiled_participant <--> rd.referent
			rd.discourse_participant_role <-- @Addressee
			b.warning.prohibited_action <--> vc.m.profiled_process
			DS <--> b.DS
			DS.speech_act <-- Admonishing
			DS.addressee <--> rd.referent

