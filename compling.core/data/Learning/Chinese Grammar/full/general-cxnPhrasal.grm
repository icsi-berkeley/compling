//============================================================
// Everything in this section has to be learned
//============================================================

//============================================================
// Constructions: Misc word classes
//============================================================

Abstract Construction Wh_Root
	subcase of Morpheme
	meaning: @Element
		evokes RD as rd
		constraints
			rd.givenness <-- @Unknown


Abstract Construction Associative_Marker
	subcase of Morpheme
	meaning
		evokes @Element as ce1
		evokes @Element as ce2


Abstract Construction Sentence_Final_Particle
	subcase of Morpheme
	meaning: Speech_Act


Abstract Construction Modal
	subcase of Preverbal_Adverbial


Abstract Construction Modal_Root
	subcase of Modal, Morpheme


Abstract Construction Conjunction
	subcase of Word


Abstract Construction Conjunction_Root
	subcase of Morpheme, Conjunction


Abstract Construction Linker
	subcase of Preverbal_Adverbial


Abstract Construction Linker_Root
	subcase of Morpheme, Linker


Abstract Construction Reflexive_Marker
	subcase of Word


Abstract Construction Negation_Marker
	subcase of Word
	meaning: Negation


Abstract Construction Negation_Root
	subcase of Morpheme, Negation_Marker


//============================================================
// Constructions: Numbers
//============================================================


Abstract Construction Number
	subcase of Word
	meaning: Numerical_Value


Construction Number_Up_To_99
	subcase of Number
	constructional
		constituents
			optional d1: Digit
			shi : shi2
			optional d2: Digit
	meaning
		constraints
			self.m.set_size <-- @Group



//============================================================
// Constructions: Nominal Expressions
//============================================================


Abstract Construction Nominal_Exp
	meaning: @Element		
	

Abstract Construction Common_Noun
	subcase of Nominal_Exp, Word


Abstract Construction CN_Compound
	subcase of Common_Noun


Construction Reduplicated_Common_Noun
	subcase of Nominal_Exp
	constructional
		constituents
			n1 : Common_Noun
			n2 : Common_Noun
		constraints
			n1 <--> n2
	form
		constraints
			n1.f meets n2.f
	meaning
		constraints
			self.m <--> n1.m
			self.m <--> n2.m


Construction NN_Compound
	subcase of Nominal_Exp
	constructional
		constituents
			n1 : Common_Noun
			n2 : Common_Noun
	form
		constraints
			n1.f meets n2.f
	meaning
		constraints
			self.m <--> n2.m	// this is ill-defined


Construction Modifier_Noun_Compound
	subcase of Nominal_Exp
	constructional
		constituents
			mo: Ref_Exp_Modifier
			n : Common_Noun [.7, .999]
	form
		constraints
			mo.f meets n.f
	meaning
		constraints
			self.m <--> n.m
			n.m <--> mo.ce


//============================================================
// Constructions: Referring Expressions
//
// examples:
// xixi
// wo3
// wo3 men
// nei3
// nei3 xie
// ren2
// yi2 ge ren2
// nei3 ge ren2
// nei3 xie ren2
// ni3 de che1
// ni3 ba4 ba3
// ni3 de nei3 tai2 che1
// wo3 zi4 ji3
//============================================================
 
 
Abstract Construction Ref_Exp
	subcase of Important_Type
	meaning: @Element
		evokes RD as rd
		constraints
			self.m <--> rd.referent


Abstract Construction Noun_Root
	subcase of Morpheme, Common_Noun
	meaning: @Element


Abstract Construction Proper_Noun
	subcase of Ref_Exp, Word
	meaning
		constraints
			rd.ontological_category <--> self.m

Abstract Construction Proper_Noun_Root
	subcase of Morpheme, Proper_Noun


Abstract Construction Pronoun
	subcase of Ref_Exp, Word		


Abstract Construction Pronoun_Root
	subcase of Morpheme, Pronoun
	meaning: @Element
		constraints
			rd.ontological_category <--> self.m


Abstract Construction Demonstrative_Root
	subcase of Morpheme, Ref_Exp


Abstract Construction Classifier
	subcase of Word
	meaning: @Element


Abstract Construction Classifier_Root
	subcase of Morpheme, Classifier


Construction CN
	subcase of Ref_Exp
	constructional
		constituents
			n: Nominal_Exp
	meaning
		constraints
			rd.ontological_category <--> n.m
			self.m <--> n.m


Construction Demonstrative_Plural
	subcase of Ref_Exp, Word
	constructional
		constituents
			p : Demonstrative_Root
			pl: xie1-C
	form
		constraints
			p.f meets pl.f
	meaning
		constraints
			rd.discourse_participant_role <--> p.rd.discourse_participant_role
			rd.set_size <-- @Group


Construction Dem_CLS
	subcase of Ref_Exp
	constructional
		constituents
			d: Demonstrative_Root
			c: Classifier
	form
		constraints
			d.f meets c.f
	meaning
		constraints
			rd <--> d.rd
			rd.ontological_category <--> c.m
			self.m <--> c.m


Construction Num_CLS_CN
	subcase of Ref_Exp
	constructional
		constituents
			optional num: Number
			c: Classifier
			n: Nominal_Exp [.7, .999]
	form
		constraints
			num.f meets c.f
			c.f meets n.f
	meaning
		constraints
			rd.ontological_category <--> c.m
			rd.ontological_category <--> n.m
			rd.givenness <-- @Context_New
			rd.set_size <--> num.m.set_size
			self.m <--> c.m
			self.m <--> n.m

	
Construction Dem_Num_CLS_CN
	subcase of Ref_Exp
	constructional
		constituents
			d: Demonstrative_Root
			optional num: Number
			c: Classifier
			n: Nominal_Exp
	form
		constraints
			d.f meets num.f
			num.f meets c.f
			c.f meets n.f			
	meaning
		constraints
			rd <--> d.rd
			rd.ontological_category <--> c.m
			rd.ontological_category <--> n.m
			rd.set_size <--> num.m.set_size
			self.m <--> c.m
			self.m <--> n.m


Construction DemPl_CN
	subcase of Ref_Exp
	constructional
		constituents
			d: Demonstrative_Plural
			n: Nominal_Exp
	form
		constraints
			d.f meets n.f
	meaning
		constraints
			rd <--> d.rd
			rd.ontological_category <--> n.m
			self.m <--> n.m


Construction Ref_Associative_Ref
	subcase of Ref_Exp
	constructional
		constituents
			r1: Ref_Exp
			de: Associative_Marker
			r2: Ref_Exp [.7, .999]
	form
		constraints
			r1.f meets de.f
			de.f meets r2.f
	meaning
		constraints
			self.m <--> r2.m
			rd <--> r2.rd
			rd.givenness <-- @Identifiable


Construction Pronoun_Ref
	subcase of Ref_Exp
	constructional
		constituents
			r1: Pronoun
			optional de: Associative_Marker
			r2: Ref_Exp
	form
		constraints
			r1.f meets de.f
			de.f meets r2.f
	meaning
		constraints
			self.m <--> r2.m
			self.m <-- @Human
			rd <--> r2.rd
			rd.givenness <-- @Identifiable


Construction Pronoun_Dem_CN
	subcase of Ref_Exp
	constructional
		constituents
			r1: Pronoun
			optional de: Associative_Marker
			r2: Dem_Num_CLS_CN
	form
		constraints
			r1.f meets de.f
			de.f meets r2.f
	meaning
		constraints
			self.m <--> r2.m
			rd <--> r2.rd
			rd.givenness <-- @Identifiable


Construction Ref_Reflexive
	subcase of Ref_Exp
	constructional
		constituents
			ne: Ref_Exp
			re: Reflexive_Marker
	form
		constraints
			ne.f meets re.f
	meaning 
		constraints
			self.m <--> ne.m
			rd <--> ne.rd


//============================================================
// Constructions: Locative Expressions
//
// The locative phrase is composed of a coverb and a 
// locative expression. These coverbs include "zai4" 
// (indicating location), "wang3" (indicating direction).
//
// The locative expression can either be a standalone 
// referring expression, a standalone locative particle
// (which expresses a spatial relation) or a referring
// expression plus a locative particle (single or compound). 
// The particles serves to foreground a part of the place.
//
// However, depending on the coverb and the placement
// of the coverb (i.e. before or after the verb), different 
// types of location expression are required.
//
// If the locative particles are used alone, some co-verbs 
// (e.g. "wang3") take both single and compound particles
// and some (e.g. "zai4") strongly prefers compound ones. 
//
//============================================================


Abstract Construction Loc_Exp
	subcase of Important_Type
	meaning: Trajector_Landmark

	
Abstract Construction Locative_Particle
	subcase of Loc_Exp, Word

Abstract Construction Demonstrative_Loc_Exp
	subcase of Loc_Exp, Word


Abstract Construction Demonstrative_Loc_Root
	subcase of Morpheme, Demonstrative_Loc_Exp
	meaning: Image_Schema


Abstract Construction Locative_Particle_Root
	subcase of Morpheme, Locative_Particle


Abstract Construction Locative_Particle_Suffix
	subcase of Morpheme
	meaning: Image_Schema
		evokes Part_Whole as partwhole


Construction CN_Participle
	subcase of Loc_Exp
	constructional
		constituents
			cn: Common_Noun
			optional loc: Locative_Particle
	form
		constraints
			cn.f meets loc.f
	meaning
		constraints
			self.m <--> loc.m
			self.m.landmark <--> cn.m


Construction Demonstrative_Loc_Exp_Compound
	subcase of Demonstrative_Loc_Exp
	constructional
		constituents
			d: Demonstrative_Loc_Root
			loc: Locative_Particle
	form
		constraints
			d.f meets loc.f
	meaning
		constraints
			self.m <--> d.m


Construction Locative_Particle_Compound	
	subcase of Locative_Particle
	constructional
		constituents
			lp: Locative_Particle_Root
			ls: Locative_Particle_Suffix
	form
		constraints
			lp.f meets ls.f
	meaning: Trajector_Landmark
		constraints
			self.m.landmark <--> ls.partwhole.whole


// combinations are limited depending on the root;
// this can't be expressed unless a number of subclasses
// of roots and suffixes are created. It would help if
// constructional subtype can condition on other constitutents.		


//============================================================
// Constructions: Verb Classes
//============================================================


// it is a linguistically dubious practice to handle A-not-A 
// and negation at the verb compound level, but it may make things easier: 
// with compound verbs, the A-not-A form is formed by
// Verb_Root - BU - Verb-Compound 
// (e.g. xi-bu-xi-huan "like not like", tiao-bu-tiao-wu "dance not dance")


Abstract Construction Verb
//	subcase of Important_Type
	constructional: Verb_Features
	meaning: Process
		evokes Speech_Act as speech_act	
		evokes Event_Structure as event_structure
		constraints
			event_structure.inherent_aspect <--> self.m.inherent_aspect


Abstract Construction Verb_Root
	subcase of Morpheme, Verb
	meaning: Process


Abstract Construction A_Not_A
	subcase of Verb 
	constructional
		constituents
			v1: Verb_Root
			n : bu4				
			v2 : Verb_Root
		constraints
			v1 <--> v2  // enforce that the same root is used			
	form
		constraints
			v1.f meets n.f
			n.f meets v2.f
	meaning: Process
		evokes Requesting_Answer as request
		constraints
			self.m <--> n.m.process
			speech_act <--> request


//============================================================
// Constructions: Aspect Markers
//============================================================


Abstract Construction Aspect_Marker
	meaning : @Modifier_Aspect
		evokes Event_Structure as event_structure
		constraints
			event_structure.modifier_aspect <--> self.m


Abstract Construction Preverbal_Aspect_Marker
	subcase of Aspect_Marker


Abstract Construction Preverbal_Aspect_Marker_Root
	subcase of Morpheme, Preverbal_Aspect_Marker


Abstract Construction Postverbal_Aspect_Marker
	subcase of Aspect_Marker


Abstract Construction Postverbal_Aspect_Marker_Root
	subcase of Morpheme, Postverbal_Aspect_Marker


//============================================================
// Constructions: Resultatives
//============================================================

Abstract Construction Postverbal_Resultative_Phrase
	meaning
		evokes Event_Structure as event_structure
		constraints
			event_structure.modifier_aspect <-- @Modifier_Perfective


Abstract Construction Motion_Resultative_Phrase
	subcase of Postverbal_Resultative_Phrase
	meaning: Source_Path_Goal


Abstract Construction Path_Particle
	subcase of Morpheme, Motion_Resultative_Phrase
	meaning: Source_Path_Goal
		evokes @Element as ce
	
	
Abstract Construction Deitic_Particle
	subcase of Morpheme, Motion_Resultative_Phrase
	meaning: Source_Path_Goal


Construction Directional_Phrase
	subcase of Motion_Resultative_Phrase
	constructional
		constituents
			pp: Path_Particle
			dp: Deitic_Particle
	form
		constraints
			pp.f meets dp.f
	meaning
		constraints
			self.m <--> pp.m 
			self.m <--> dp.m


Construction Modified_Goal_Phrase
	subcase of Motion_Resultative_Phrase
	constructional
		constituents
			pp: Path_Particle
			np : Ref_Exp
	form
		constraints
			pp.f meets np.f   // not sure if this is too restrictive
	meaning
		constraints
			self.m <--> pp.m
			np.m <--> pp.ce


Construction Locative_Phrase
	subcase of Motion_Resultative_Phrase
	constructional
		constituents
			cv : zai4-CV
			optional post_asp : Postverbal_Aspect_Marker
			np : Ref_Exp
			optional dp : Directional_Phrase
	form
		constraints
			cv.f meets post_asp.f
			post_asp.f meets np.f
			np.f meets dp.f
	meaning: Source_Path_Goal
		constraints
			self.m.goal <--> cv.m.landmark
			cv.ce <--> np.m


// Li & Thompson calls this the directional phrase, which is a little confusing

Construction Destinational_Phrase
	subcase of Motion_Resultative_Phrase
	constructional
		constituents
			cv : dao4-CV
			optional post_asp : Postverbal_Aspect_Marker
			np : Ref_Exp
			optional dp : Directional_Phrase
	form
		constraints
			cv.f meets post_asp.f
			post_asp.f meets np.f
			np.f meets dp.f
	meaning: Source_Path_Goal
		constraints
			self.m <--> cv.m
			cv.ce <--> np.m

// Unfortunately there is also a "extent of action" reading of the dao4-phrase, denoting that an
// action is carried out until it reaches a certain state (e.g. kan4 dao4 di4 san1 ye4, "reading up to the third page")


Abstract Construction Stative_Resultative_Phrase
	subcase of Postverbal_Resultative_Phrase
	meaning: State
//		evokes Cause_Effect as ce
//		constraints
//			ce.effect <--> self.m


// I'm keeping this unary rule for now - otherwise the multiple uses of a Verb is very confusing to understand

Construction Resultative_Marker
	subcase of Stative_Resultative_Phrase
	constructional
		constituents
			v : Verb
	meaning: State
		constraints
			self.m <--> v.m
			event_structure <--> v.event_structure

//============================================================
// Constructions: Other Coverb Modifications
//============================================================


Abstract Construction Coverb
	subcase of Morpheme
	meaning
		evokes @Element as ce


Abstract Construction Locational_Coverb
	subcase of Coverb
	meaning: Image_Schema


Abstract Construction Special_Coverb
	subcase of Coverb


Abstract Construction Coverb_Phrase


Abstract Construction Preverbal_Coverb_Phrase
	subcase of Coverb_Phrase, Important_Type


//FIXME: need separate wang3 phrases for Absolute_Direction and Relative_Direction

Construction Preverbal_Heading_Phrase
	subcase of Preverbal_Coverb_Phrase
	constructional
		constituents
			wang : wang3-CV
			loc : Loc_Exp
	form
		constraints
			wang.f meets loc.f			
	meaning: Source_Path_Goal
		constraints
			self.m <--> wang.m
			self.m.goal <--> loc.m.landmark


Construction Preverbal_Locative_Phrase
	subcase of Preverbal_Coverb_Phrase
	constructional
		constituents
			cv : Locational_Coverb
			loc : Loc_Exp
	form
		constraints
			cv.f meets loc.f			
	meaning: Trajector_Landmark
		constraints
			self.m <--> cv.m
			cv.ce <--> loc.m.landmark


Construction Preverbal_Instrumental_Phrase
	subcase of Preverbal_Coverb_Phrase
	constructional
		constituents
			cv : yong4-CV
			np : Ref_Exp
	form
		constraints
			cv.f meets np.f
	meaning
		evokes Process as process
		constraints
			cv.ce <--> np.m
			cv.process <--> process 


Construction Preverbal_Gei3_Phrase
	subcase of Preverbal_Coverb_Phrase
	constructional
		constituents
			cv : gei3-CV1
			np : Ref_Exp
	form
		constraints
			cv.f meets np.f			
	meaning: BenefactionOrMalefaction
		evokes @Element as protagonist
		evokes Process as process
		constraints
			self.m <--> cv.m
			self.m.beneficiary <--> np.m
			self.m.benefactor <--> protagonist
			self.m.benefit <--> process


//============================================================
// Constructions: Modifiers
//============================================================


Abstract Construction Modifier
	meaning: Modification


Abstract Construction Preverbal_Adverbial
	subcase of Modifier


Abstract Construction Preverbal_Adverbial_Root
	subcase of Morpheme, Preverbal_Adverbial


Abstract Construction Ref_Exp_Modifier
	subcase of Modifier
	meaning
		evokes @Element as ce


Abstract Construction Ref_Exp_Modifier_Root
	subcase of Ref_Exp_Modifier


Abstract Construction Nominalizer
	subcase of Morpheme
	meaning
		evokes @Element as ce


Construction Preverbal_Adverbial_Phrase
	subcase of Preverbal_Adverbial
	constructional
		constituents
			v : Verb	// this might be overly exclusive
			de : Associative_Marker
	form
		constraints
			v.f meets de.f
	meaning
		evokes Intransitive_State as ins
		constraints
			ins <--> v.m
			self.m.category <--> ins.category
	
	
Construction Stative_Associative
	subcase of Ref_Exp_Modifier
	constructional
		constituents
			v: Verb
			de: Nominalizer
	form
		constraints
			v.f meets de.f
	meaning
		evokes Intransitive_State as ins
		constraints
			ins <--> v.m
			ce <--> de.ce
			ce <--> v.m.protagonist
			self.m.category <--> ins.category


//============================================================
// Constructions: Exclamatories
//============================================================


Abstract Construction Interjection
	subcase of Important_Type


Abstract Construction Interjection_Morpheme
	subcase of Morpheme, Interjection


Construction Chained_Interjections
	subcase of Interjection
	constructional
		constituents
			in1 : Interjection
			in2 : Interjection
	form
		constraints
			in1.f meets in2.f	