
// Interaction CxN


general construction FDArgStruct  //  helped John to move
   subcase of ArgumentStructure
   constructional
     constituents
        v: Verb // LexicalVerb? ************ TODO
        np: NP
        vp: VP
     constraints
        vp.features.verbform <-- NonFinite
        v.features.verbkind <-- ForceDynamic
	    self.features <--> v.features // inherited
	    v.features.expectedVerbForm <--> vp.features.verbform // test
   form 
     constraints
       	v.f before np.f
 		np.f before vp.f
   meaning: AgentAgentProcess  
     constraints
       	self.m.process1 <--> v.m
       	self.m.process1.event <--> vp.ed
       	self.m.protagonist <--> ed.profiledParticipant // from ArgStruct
       	self.m.other  <--> vp.ed.profiledParticipant
		self.m.process2 <--> vp.m 
        self.m.other <--> np.m
       
 
construction FDArgStructInf  //  helped John to move (Box1)
   subcase of FDArgStruct
   constructional
     constraints
       // vp.features.verbform <-- Infinitive no longer needed
        

construction FDArgStructFrom  //  stopped John from moving
   subcase of FDArgStruct
   constructional
     constituents
        v: Verb // special type 
        np: NP
	    tag1: FROM-Marker 
        vp: VP
     constraints
        vp.features.verbform <-- PresentParticiple  //not needed
   form 
     constraints
       	v.f before np.f
 		np.f before tag1.f
		tag1.f before vp.f
 

general construction ActiveTransitiveCEA
  subcase of ArgumentStructure
  constructional
    constituents
      np: NP [1.0, .9]
  form
    constraints
      v.f before np.f
  meaning: CauseEffectAction 
    constraints
      self.m.affected <--> np.m
	  np.m.ontological-category <-- @entity  // new!! change for metaphors


// causative action, Causer profiled
// e.g.  he hit the table
//
construction ActiveTransitiveProfiledCauser
  subcase of ActiveTransitiveCEA
  meaning: CauseEffectAction 
    evokes ForcefulMotionAction as fma
    constraints
      v.m <--> fma
      self.m.process1 <--> fma.process1
      self.m.actionary <--> fma.routine
      self.m.causer <--> ed.profiledParticipant 

// causative action,  Instrument profiled
// e.g.  The hammer hit the table
//
construction ActiveTransitiveProfiledInstrument
  subcase of ActiveTransitiveCEA
  meaning: CauseEffectAction   
    evokes ForcefulMotionAction as fma
    constraints
      v.m <--> fma
      self.m.process1 <--> fma.process1
      self.m.actionary <--> fma.routine
      //
      self.m.process1.instrument <--> ed.profiledParticipant


// Agentively caused process, Causer profiled
// e.g.  He slid the box
//
construction ActiveTransitiveProfiledCauserResultProcess
  subcase of ActiveTransitiveCEA
  meaning: CauseEffectAction 
    evokes Motion as mp
    constraints
      mp <--> v.m
      self.m.process2 <--> mp
      self.m.causer <--> ed.profiledParticipant 

// Passive CEA
//
general construction PassiveCEA
  subcase of ArgumentStructure
  constructional
    constituents
       optional byPhrase: ByAgentPhrase
    constraints
       //v.features.verbform <-- PastParticiple
  form
    constraints
      v.f before byPhrase.f
  meaning: CauseEffectAction
    constraints
       	self.m.affected <--> ed.profiledParticipant
		v.m.p-features.voice <-- @passive	

construction PassiveCEACauserBYPhrase  // require byPhrase ??
  subcase of PassiveCEA
  meaning
      evokes ForcefulMotionAction as fma
    constraints
      v.m <--> fma
      self.m.process1 <--> fma.process1
      self.m.causer <--> byPhrase.m.landmark
      self.m.actionary <--> fma.routine

construction PassiveCEAInstrumentBYPhrase
  subcase of PassiveCEA
  meaning
      evokes ForcefulMotionAction as fma
    constraints
      v.m <--> fma
      self.m.process1 <--> fma.process1
      self.m.process1.instrument <--> byPhrase.m.landmark
      self.m.actionary <--> fma.routine

construction PassiveCEACauserBYPhraseResultProcess
  subcase of PassiveCEA
  meaning
    evokes Motion as mp
    constraints
      mp <--> v.m
      self.m.process2 <--> mp
      self.m.causer <--> byPhrase.m.landmark      
      
