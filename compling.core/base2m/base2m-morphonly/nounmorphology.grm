/*********************
 Compositional morphology for nominals
 *********************
 nschneid
 2009-05-13
*/

/*
construction PluralSNoun
	subcase of SSuffix, RegularCommonNoun, PluralNoun
	constructional
	  constituents
		stem: RegularCommonNoun
	  constraints
		stem.features.number <-- Sg
		stem.features.massOrPlural <-- "no"
	meaning
	  constraints
//	    ignore stem.m.number <-- "1"
		self.m <--> stem.m
*/
		
general construction InflectedNoun
	subcase of Noun
	constructional
      constituents
	    infl: NounInflection
	  constraints
	    self.features <--> infl.features
	    self.atWordLevel <--> infl.atWordLevel
	meaning constraints
	  self.m.number <--> infl.m.number
	  
general construction RegularInflectedNoun
	subcase of InflectedNoun
	constructional
      constituents
	    infl: RegularNounInflection
	form: WLFinalSibilantForm
	  constraints
	    self.f.finalSib <-- ""	// to avoid overgenerating the -es suffix
	    self.f.finalSib <--> infl.f.finalSib
	    self.f.stem before infl.f.suffix
	    self.f.stem meets infl.f.suffix

general construction RegularSimpleInflectedNoun
	subcase of RegularInflectedNoun
	constructional constituents
	  infl: RegularSimpleNounInflection

general construction RegularFinalSibInflectedNoun
	subcase of RegularInflectedNoun
	form: WLFinalSibilantForm
	  constraints
	    ignore self.f.stem meets infl.f.suffix
	    ignore self.f.finalSib <-- ""
	    self.f.stem meets self.f.finalSib
	    self.f.finalSib before infl.f.suffix
	    self.f.finalSib meets infl.f.suffix

general construction RegularFinalYNoun
	subcase of RegularInflectedNoun
	constructional
	  constituents
	    infl: RegularFinalYNounInflection
	form: WLFinalSibilantFinalYForm
	  constraints
	    ignore self.f.stem meets infl.f.suffix
	    self.f.stem meets infl.f.finalY
	    infl.f.finalY meets infl.f.suffix




general construction NounInflection
	subcase of HasNominalFeatures, Morph
	form: SuffixForm
	meaning: NounInflectionMeaning
	
construction InternalNounInflection
  subcase of RegularSimpleNounInflection, RegularFinalYNounInflection
  constructional constraints
    self.atWordLevel <-- "no"
  form constraints
    self.f.finalY <-- ""
    self.f.suffix <-- ""
	
general construction SingularNounInflection
  subcase of NounInflection, Singular
   constructional
     constraints
       self.features.massOrPlural <-- "no"
       self.atWordLevel <-- "yes"
   meaning
    constraints
      self.m.number <-- "1"
      
general construction PluralNounInflection
  subcase of NounInflection, Plural
   constructional
     constraints
       self.features.massOrPlural <-- "yes"
       self.atWordLevel <-- "yes"
   meaning
    constraints
      self.m.number <-- ">1"
	
general construction RegularNounInflection
	subcase of NounInflection
	form: WLFinalSibilantFinalYForm
	
general construction RegularSimpleNounInflection
	subcase of RegularNounInflection

construction RegularNounSingular
	subcase of RegularSimpleNounInflection, RegularFinalYNounInflection, SingularNounInflection
	form constraints
	  self.f.finalY <-- "y"
	  self.f.suffix <-- ""
	
construction RegularSimpleNounPlural
	subcase of RegularSimpleNounInflection, PluralNounInflection
	form constraints
	  self.f.finalSib <-- ""
	  self.f.finalY <-- ""
	  self.f.suffix <-- "s"

	
general construction RegularFinalYNounInflection
	subcase of RegularNounInflection
	
construction RegularFinalYNounPlural
	subcase of RegularFinalYNounInflection, Plural
	form constraints
	  self.f.finalSib <-- ""
	  self.f.finalY <-- ""
	  self.f.suffix <-- "ies"

general construction RegularFinalSibilantPlural	// the stem ends in a sibilant
	subcase of RegularNounInflection, Plural
	form constraints
	  self.f.suffix <-- "es"
	   
construction FinalSPlural
	subcase of RegularFinalSibilantPlural
	form constraints
	  self.f.finalSib <-- "s"
	  
construction FinalZPlural
	subcase of RegularFinalSibilantPlural
	form constraints
	  self.f.finalSib <-- "z"
	  
construction FinalXPlural
	subcase of RegularFinalSibilantPlural
	form constraints
	  self.f.finalSib <-- "x"
	  
construction FinalSHPlural
	subcase of RegularFinalSibilantPlural
	form constraints
	  self.f.finalSib <-- "sh"
	  
construction FinalCHPlural
	subcase of RegularFinalSibilantPlural
	form constraints
	  self.f.finalSib <-- "ch"
	  
	