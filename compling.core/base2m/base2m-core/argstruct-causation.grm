general construction ActiveTransitiveCEA
  subcase of ArgumentStructure
  constructional
    constituents
      np: NP [1.0, .9]
    constraints
      np.features.case <-- "acc"	// nschneid
  form
    constraints
      v.f before np.f
  meaning: CauseEffectAction 
    constraints
      self.m.affected <--> np.m.referent





// causative action, Causer profiled
//e.g.  he hit the table

construction ActiveTransitiveProfiledCauser
  subcase of ActiveTransitiveCEA
  meaning: CauseEffectAction 
    evokes ForcefulMotionAction as fma
    constraints
      v.m <--> fma
      self.m.process1 <--> fma.process1
      self.m.causer <--> ed.profiledParticipant 
      self.m.x-net <--> fma.routine


construction ActiveTransitiveComplexVerb	// nschneid: like ActiveTransitiveProfiledCauser, except with a causative verb
  subcase of ActiveTransitiveCEA
  constructional constituents
    v: CausativeVerb
  meaning: CauseEffectAction 
    constraints
      self.m <--> v.m
      self.m.causer <--> ed.profiledParticipant 




// causative action,  Instrument profiled
//e.g.  The hammer hit the table

construction ActiveTransitiveProfiledInstrument
  subcase of ActiveTransitiveCEA
  meaning: CauseEffectAction   
    evokes ForcefulMotionAction as fma
    constraints
      v.m <--> fma
      self.m.process1 <--> fma.process1
      self.m.process1.instrument <--> ed.profiledParticipant
      self.m.x-net <--> fma.routine

// Agentively caused process, Causer profiled
//e.g.  He slid the box

construction ActiveTransitiveProfiledCauserResultProcess
  subcase of ActiveTransitiveCEA
  meaning: CauseEffectAction 
    evokes Motion as mp
    constraints
      mp <--> v.m
      self.m.process2 <--> mp
      self.m.causer <--> ed.profiledParticipant 








//Passive CEA
general construction PassiveCEA
  subcase of ArgumentStructure, RootType
  constructional
    constituents
       optional byPhrase:ByPhrase
    constraints
       v.features.verbform <-- PastParticiple
  form
    constraints
      v.f before byPhrase.f
  meaning:CauseEffectAction
    constraints
       self.m.affected <--> ed.profiledParticipant

general construction PassiveCauserBYPhrase
  subcase of PassiveCEA
  meaning
    constraints
      self.m.causer <--> byPhrase.m.landmark


construction PassiveCEACauserBYPhrase
  subcase of PassiveCauserBYPhrase
  meaning
      evokes ForcefulMotionAction as fma
    constraints
      self.m.process1 <--> fma.process1
      v.m <--> fma
      self.m.x-net <--> fma.routine

construction PassiveCEAInstrumentBYPhrase
  subcase of PassiveCEA
  meaning
      evokes ForcefulMotionAction as fma
    constraints
      v.m <--> fma
      self.m.process1 <--> fma.process1
      self.m.process1.instrument <--> byPhrase.m.landmark
      self.m.x-net <--> fma.routine

construction PassiveCEACauserBYPhraseResultProcess
  subcase of PassiveCauserBYPhrase
  meaning
    evokes Motion as mp
    constraints
      mp <--> v.m
      self.m.process2 <--> mp

general construction PassiveCEAComplexVerb	// nschneid: case where the verb's meaning is itself a CauseEffectAction, as in Institutionalize
  subcase of PassiveCEA
  constructional constituents
    v: CausativeVerb
  meaning
    constraints
      self.m <--> v.m

construction PassiveCEAComplexVerbCauserBYPhrase	// nschneid
  subcase of PassiveCEAComplexVerb, PassiveCauserBYPhrase
